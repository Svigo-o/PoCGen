plugins {
    id 'java'
}

group = 'com.svigo.pocgen'
version = '0.2.0'

description = 'Burp Suite extension exposing HTTP API for PoCGen automation'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly files('libs/burp-extender-api.jar')
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

jar {
    archiveBaseName.set('burp-extender-llm-controller')
    manifest {
        attributes(
                'Implementation-Title': 'LLM Proxy Controller',
                'Implementation-Version': project.version,
                'Built-By': System.properties['user.name'],
                'Built-JDK': System.getProperty('java.version'),
                'Created-By': "Gradle ${gradle.gradleVersion}"
        )
    }
}

// Optional fat jar if runtime dependencies are added later
tasks.register('fatJar', Jar) {
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.output
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest.from(jar.manifest)
}
